#!/bin/csh -f
#
# $Header: /u/gcmpack/MITgcm/tools/Attic/genmake.hp,v 1.2 1998/04/23 21:00:52 cnh Exp $
#
#  Makefile generator for MITgcm UV codes
#

# Generate make files for HP SPP system
set mfile = ( Makefile.hp      )
set mach  = ( 'HP  HP-UX/spp '    )

# Directories for source, includes, binaries and executables
# Note
# o If you prefer/need everything under a single directory
#   copy everything in ../psupp/src, ../model/src,
#   ../psupp/inc and ../model/inc into a directory and then
#   edit the paths below to read
#   set PSUPP_SDIR = (  )
#   set MODEL_SDIR = (  )
#   set PSUPP_IDIR = (  )
#   set MODEL_IDIR = (  )
#   set TDIR       = (  )
#   set BDIR       = (  )
#   set EDIR       = (  )
set PSUPP_SDIR = ( ../eesupp/src/ )
set MODEL_SDIR = ( ../model/src/ )
set PSUPP_IDIR = ( ../psupp/inc/ )
set MODEL_IDIR = ( ../model/inc/ )
set TDIR       = ( ../tools/ )
set BDIR       = ( ../bin/       )
set EDIR       = ( ../exe/       )
set EXE        = ( barrier       )

set CPP   = ( '/usr/ccs/lbin/cpp -P $(INCLUDES) -DUSE_EXEMPLAR' )
set FCOMP = ( 'f77 +O4 -c +U77 +Oparallel_env +Oparallel +Onoautopar ' \
	      '+Oexemplar_model +Okernel_threads +Oreport=all'  )
set FCOMP0 = ( 'f77 +O0 -c +U77 +Onoautopar ' \
	       '+Oexemplar_model +Okernel_threads ' )
set LINK  = ( 'f77 +O4 +U77 +Oparallel_env +Oparallel +Onoautopar ' \
	      '+Oexemplar_model +Okernel_threads' )
set INCLUDES = ( '-I. -I$(PSUPP_IDIR) -I$(MODEL_IDIR) ' \
                 '-I/usr/local/mpi/include' )
set MAKEDEPEND = ( /opt/imake/bin/makedepend )

echo "Creating make file ${mfile} "
echo "#"                    > ${mfile}
echo "# Multithreaded + multi-processing makefile for $mach "      >> ${mfile}
echo "# This make file was generated automatically by the command" >> ${mfile}
echo "# " \"${0}\"                                                 >> ${mfile}
echo "# on `hostname`:`pwd` "                                      >> ${mfile}
echo "#  " `date`                                                  >> ${mfile}
echo "# by $user"                                                  >> ${mfile}
echo "# Notes    "                                                 >> ${mfile}
echo "# =====    "                                                 >> ${mfile}
echo "# Exemplar parallel threads ar controlled by"                >> ${mfile}
echo "# setenv MP_NUMBER_OF_THREADS"                               >> ${mfile}
echo "#         "                                                  >> ${mfile}

cat >> ${mfile} <<EOF

# PSUPP_SDIR: Directory holding parallel support routines
# MODEL_SDIR: Directory model code
# PSUPP_IDIR: Directory holding parallel support header files
# MODEL_IDIR: Directory model header files
# TDIR      : Directory where build tools are stored
# BDIR      : Directory where object files are written
# EDIR      : Directory where executable that is generated is written
# INCLUDES  : Directories searched for header files
# CPP       : C-preprocessor command               
# FCOMP     : Fortran compiler command             
# LINK      : Command for link editor program          
# Note           
# ====
# o PSUPP_SDIR ... EDIR can all be set to the current directory if
#   you want to organise things that way. Alternatively they can be
#   set to different directories so that code can be kept on a backed
#   up disk while object files and executables are created on scratch
#   disks. I prefer this latter setup!
# o Under Solaris if the disk on which the compiler is generating 
#   object files and executables is mounted via NFS rather than being
#   a local SCSI disk compilation times can increase by a factor of
#   ten!
PSUPP_SDIR = ${PSUPP_SDIR}
MODEL_SDIR = ${MODEL_SDIR}
PSUPP_IDIR = ${PSUPP_IDIR}
MODEL_IDIR = ${MODEL_IDIR}
TDIR       = ${TDIR}
BDIR       = ${BDIR}  
EDIR       = ${EDIR}
INCLUDES   = ${INCLUDES}
EXE        = \$(EDIR)${EXE}
EOF

echo "# C preprocessor   " >> ${mfile}
echo 'CPP        =' ${CPP} >> ${mfile}
echo "# Fortran compiler " >> ${mfile}
echo 'FCOMP  = '${FCOMP}   >> ${mfile}
echo 'FCOMP0 = '${FCOMP0}  >> ${mfile}
echo "# Link editor      " >> ${mfile}
echo 'LINK  = '${LINK}     >> ${mfile}
echo "# Header dependency" >> ${mfile}
echo 'MAKEDEPEND  = '${MAKEDEPEND}   >> ${mfile}
echo "                   " >> ${mfile}


set psupp_files = ( )
set psupp_slist = ( )
set psupp_olist = ( )
if ( ${PSUPP_SDIR} != ${MODEL_SDIR} ) then
 set flist = `ls -1 ${PSUPP_SDIR} | grep '.*\.F'`
 if ( "${flist}" != "" ) then
  foreach f ( $flist )
   set fname = ( ${f:t}       )
   set pf    = ( ${fname:r}.f )
   set of    = ( ${fname:r}.o )
   echo ${pf}: '$(PSUPP_SDIR)'${fname}   >> ${mfile}.$$
   echo '	@echo Preprocessing $(PSUPP_SDIR)'${fname} >> ${mfile}.$$
   echo '	@ $(CPP) $(PSUPP_SDIR)'${fname}' > '$pf >> ${mfile}.$$
   echo ${of}: ${pf}  >> ${mfile}.$$
   echo '	@echo Compiling '${pf} >> ${mfile}.$$
#  Compile fbar.F without optimisation. This file holds the barrier
#  routines which rely on shared variables which can be accessed
#  concurrently. Optimisation knows nothing about parallel access
#  it assumes sequential semantics and rearranges code on that 
#  basis.
   if ( "${fname}" != "fbar.F" ) then
    echo '	@ $(FCOMP) '$pf >> ${mfile}.$$
   else
    echo '	@ $(FCOMP0) '$pf >> ${mfile}.$$
   endif   
   set psupp_files = ( ${psupp_files} ${fname}  )
   set psupp_slist = ( ${psupp_slist} ${f}  )
   set psupp_olist = ( ${psupp_olist} ${of} )
  end
 endif
endif

set model_files = ( )
set model_slist = ( )
set model_olist = ( )
set flist = `ls -1 ${MODEL_SDIR} | grep '.*\.F'`
if ( "${flist}" != "" ) then
 foreach f ( ${flist} )
  set fname = ( ${f:t}       )
  set pf    = ( ${fname:r}.f )
  set of    = ( ${fname:r}.o )
  echo ${pf}: '$(MODEL_SDIR)'${fname}   >> ${mfile}.$$
  echo '	@echo Preprocessing $(MODEL_SDIR)'${fname} >> ${mfile}.$$
  echo '	@ $(CPP) $(MODEL_SDIR)'${fname}' > '$pf >> ${mfile}.$$
  echo ${of}: ${pf}  >> ${mfile}.$$
  echo '	@echo Compiling '${pf} >> ${mfile}.$$
  echo '	@ $(FCOMP) '$pf >> ${mfile}.$$
  set model_files = ( ${model_files} ${fname}  )
  set model_slist = ( ${model_slist} ${f}  )
  set model_olist = ( ${model_olist} ${of} )
 end
endif

echo "                   " >> ${mfile}
echo "# ===========      " >> ${mfile}
echo "# File lists       " >> ${mfile}
echo "# ===========      " >> ${mfile}
if ( $#psupp_olist == 0 ) then
 echo 'psupp_objs = ' >> ${mfile}
else if ( $#psupp_olist == 1 ) then
 echo 'psupp_objs = '${psupp_olist} >> ${mfile}
else
 echo 'psupp_objs = '$psupp_olist[1] '\' >> ${mfile}
 set psupp_count = 2
 while ( $psupp_count < $#psupp_olist )
  echo '             '$psupp_olist[$psupp_count] '\' >> ${mfile}
  @ psupp_count = $psupp_count + 1
 end
 echo '             '$psupp_olist[$#psupp_olist] >> ${mfile}
endif
echo "                   " >> ${mfile}
if ( $#psupp_slist == 0 ) then
 echo 'psupp_srcs = ' >> ${mfile}
else if ( $#psupp_slist == 1 ) then
 echo 'psupp_srcs = $(PSUPP_SDIR)'${psupp_slist} >> ${mfile}
else
 echo 'psupp_srcs = $(PSUPP_SDIR)'$psupp_slist[1] '\' >> ${mfile}
 set psupp_count = 2
 while ( $psupp_count < $#psupp_slist )
  echo '             $(PSUPP_SDIR)'$psupp_slist[$psupp_count] '\' >> ${mfile}
  @ psupp_count = $psupp_count + 1
 end
 echo '             $(PSUPP_SDIR)'$psupp_slist[$#psupp_slist] >> ${mfile}
endif
echo "                   " >> ${mfile}
if ( $#psupp_files == 0 ) then
 echo 'psupp_sfil = ' >> ${mfile}
else if ( $#psupp_files == 1 ) then
 echo 'psupp_sfil = '${psupp_files} >> ${mfile}
else
 echo 'psupp_sfil = '$psupp_files[1] '\' >> ${mfile}
 set psupp_count = 2
 while ( $psupp_count < $#psupp_files )
  echo '             '$psupp_files[$psupp_count] '\' >> ${mfile}
  @ psupp_count = $psupp_count + 1
 end
 echo '             '$psupp_files[$#psupp_files] >> ${mfile}
endif
echo "                   " >> ${mfile}
if ( $#model_olist == 0 ) then
 echo 'model_objs = ' >> ${mfile}
else if ( $#model_olist == 1 ) then
 echo 'model_objs = '${model_olist} >> ${mfile}
else
 echo 'model_objs = '$model_olist[1] '\' >> ${mfile}
 set model_count = 2
 while ( $model_count < $#model_olist )
  echo '             '$model_olist[$model_count] '\' >> ${mfile}
  @ model_count = $model_count + 1
 end
 echo '             '$model_olist[$#model_olist] >> ${mfile}
endif
echo "                   " >> ${mfile}
if ( $#model_slist == 0 ) then
 echo 'model_srcs = ' >> ${mfile}
else if ( $#model_slist == 1 ) then
 echo 'model_srcs = $(MODEL_SDIR)'${model_slist} >> ${mfile}
else
 echo 'model_srcs = $(MODEL_SDIR)'$model_slist[1] '\' >> ${mfile}
 set model_count = 2
 while ( $model_count < $#model_slist )
  echo '             $(MODEL_SDIR)'$model_slist[$model_count] '\' >> ${mfile}
  @ model_count = $model_count + 1
 end
 echo '             $(MODEL_SDIR)'$model_slist[$#model_slist] >> ${mfile}
endif
echo "                   " >> ${mfile}
if ( $#model_files == 0 ) then
 echo 'model_sfil = ' >> ${mfile}
else if ( $#model_files == 1 ) then
 echo 'model_sfil = '${model_files} >> ${mfile}
else
 echo 'model_sfil = '$model_files[1] '\' >> ${mfile}
 set model_count = 2
 while ( $model_count < $#model_files )
  echo '             '$model_files[$model_count] '\' >> ${mfile}
  @ model_count = $model_count + 1
 end
 echo '             '$model_files[$#model_files] >> ${mfile}
endif
echo "                   " >> ${mfile}
echo 'objs = $(psupp_objs) $(model_objs)' >> ${mfile}
echo 'srcs = $(psupp_srcs) $(model_srcs)' >> ${mfile}
echo 'sfil = $(psupp_sfil) $(model_sfil)' >> ${mfile}
echo "                   " >> ${mfile}
echo "# ===========      " >> ${mfile}
echo "# Begin rules      " >> ${mfile}
echo "# ===========      " >> ${mfile}
echo "                   " >> ${mfile}
echo '$(EXE): $(objs)    ' >> ${mfile}
echo '	$(LINK) -o $(EXE) $(objs)' >> ${mfile}
echo "                   " >> ${mfile}
echo 'clean:  ' >> ${mfile}
echo '	rm *.o *.f *.out *.cmp   ' >> ${mfile}
echo "                   " >> ${mfile}
echo 'depend:  ' >> ${mfile}
echo '	@-ln -s $(srcs) . ;$(MAKEDEPEND) -o .f -f $(TDIR)'${mfile}' $(INCLUDES) $(sfil) ' >> ${mfile}
echo "                   " >> ${mfile}
cat ${mfile}.$$ >> ${mfile}
\rm ${mfile}.$$

# $ Id: $
